# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

let directory = 'code/'

let rules = [
  { pattern: '".-"', capture: ["string"] },
  { pattern: "'.-'", capture: ["string"] },
  { pattern: "#.*$", capture: ["comment"] },
  { pattern: "(let)[%s%p]+([%w-]+)[%s%p]", capture: [none, "keyword", "entity"] },
  { pattern: "(func)[%s%p]+([%w-]+)[%s%p]", capture: [none, "keyword", "entity"] },
  { pattern: "(copy)[%s%p]", capture: [none, "constant"] },
  { pattern: "(clear)[%s%p]", capture: [none, "constant"] },
  { pattern: "(delete)[%s%p]", capture: [none, "constant"] },
  { pattern: "(float)[%s%p]", capture: [none, "constant"] },
  { pattern: "(index)[%s%p]", capture: [none, "constant"] },
  { pattern: "(int)[%s%p]", capture: [none, "constant"] },
  { pattern: "(insert)[%s%p]", capture: [none, "constant"] },
  { pattern: "(inspect)[%s%p]", capture: [none, "constant"] },
  { pattern: "(keys)[%s%p]", capture: [none, "constant"] },
  { pattern: "(pop)[%s%p]", capture: [none, "constant"] },
  { pattern: "(push)[%s%p]", capture: [none, "constant"] },
  { pattern: "(echo)[%s%p]", capture: [none, "constant"] },
  { pattern: "(print)[%s%p]", capture: [none, "constant"] },
  { pattern: "(str)[%s%p]", capture: [none, "constant"] },
  { pattern: "(exists)[%s%p]", capture: [none, "constant"] },
  { pattern: "(type)[%s%p]", capture: [none, "constant"] },
  { pattern: "(len)[%s%p]", capture: [none, "constant"] },
  { pattern: "(true)[%s%p]", capture: [none, "constant"] },
  { pattern: "(false)[%s%p]", capture: [none, "constant"] },
  { pattern: "(none)[%s%p]", capture: [none, "constant"] },
  { pattern: "(use)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(let)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(if)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(elif)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(else)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(for)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(while)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(return)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(break)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(continue)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(in)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(try)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(except)[%s%p]", capture: [none, "keyword"] },
  { pattern: "(throw)[%s%p]", capture: [none, "keyword"] },
  { pattern: ">>>", capture: ["keyword"] },
  { pattern: "<<", capture: ["keyword"] },
  { pattern: ">>", capture: ["keyword"] },
  { pattern: "->", capture: ["keyword"] },
  { pattern: "==", capture: ["keyword"] },
  { pattern: "!=", capture: ["keyword"] },
  { pattern: "[!=><&~/+-*^]", capture: ["keyword"] },
  { pattern: "%d+%.%d+", capture: ["constant"] },
  { pattern: "%d+", capture: ["constant"] }
]

func escape(input) {
  input = text.replace(input, "<", "&lt;")
  input = text.replace(input, ">", "&gt;")
  input = text.replace(input, '"', "&quot;")
  return input
}

func code-highlight(path) {
  echo path
  let source = io.read-lines(directory + path)
  if !text.ends(path, '.hm') {
    echo "ok!"
    return escape(source)
  }
  out = []
  for line in source {
    if line == "" { push(out, "") continue }
    html = ""
    while true {
      let top = none
      let left = none
      for rule in rules {
        let match = pattern.find(line, rule.pattern)
        if match == none { continue }
        let zero = match[0]
        if zero > 0 and !pattern.match(line[zero - 1], "[%s%p]") { continue }
        if top == none or zero < left[0] {
          top = rule
          left = match
          if zero == 0 { break }
        }
      }
      if top == none { break }
      let match = left
      let capture = top.capture
      let count = len(capture)
      if count == 1 {
        if match[0] > 0 { html += escape(line[:match[0]]) }
        html += "<" + capture[0] + ">" + escape(line[match[0]:match[1]]) + "</" + capture[0] + ">"
        if match[1] >= len(line) { line = "" break }
        line = line[match[1]:]
      } else {
        if len(match) < 2 { throw "missing capture group: ${match} / ${capture} (${top.pattern})" }
        if match[2] > 0 { html += escape(line[:match[2]]) }
        let g = 0
        for i = 1, i < count {
          g = i * 2
          if g >= len(match) { throw "missing capture group: ${match} / ${capture} (${top.pattern})" }
          if i != 1 {
            html += escape(line[match[g - 1]:match[g]])
          }
          html += "<" + capture[i] + ">" + escape(line[match[g]:match[g + 1]]) + "</" + capture[i] + ">"
        }
        if match[g + 1] >= len(line) { line = "" break }
        line = line[match[g + 1]:]
      }
    }
    html += escape(line)
    push(out, html)
  }
  return text.join(out, '\n')
}
